module Gallina

imports
  Lexical
  Qid
  Options
  extensions/Function

context-free syntax // Figure 1.1 of the reference manual 
 
	Term.ForAll   = <forall <Binder+>, <Term>>
	Term.Fun      = [fun [Binder+] => [Term]]
	Term.Fix      = <fix <FixBodies>>
	Term.CoFix    = <cofix <CoFixBodies>>
	Term.Let      = <
	let <Ident><OptBinders><OptType> := 
	  <Term>
	in
	  <Term>
	>
	Term.LetFix   = <let fix <FixBody> in <Term>>
	Term.LetCoFix = <let fix <CoFixBody> in <Term>>
	Term.LetTuple = <let (<{Name ", "}*>)<OptDepRetType> := <Term> in <Term>>
	Term.LetPat   = <let <SingleQuote> <Pattern><OptInTerm> := <Term><OptReturnType> in <Term>>
	Term.If       = <if <Term><OptDepRetType> then <Term> else <Term>>
		
	Term.Cast     = <<Term> : <Term>>  {non-assoc}
	Term.Check    = [[Term] <: [Term]] {non-assoc}
	Term.Coerce   = [[Term] :>]        {non-assoc}
	Term.Arrow    = [[Term] -> [Term]] {right}
	
	Term.Apply     = <<Term> <Term>> {left, prefer}
	Term.ApplyImplicit = <<Term> <Arg>>
	
	Term.UnHide   = <@<QualId><OptTerms>>

  Term.ScopeRef = <<Term> % <Ident>>
	 
  Term.Match    = <
  match <{MatchItem ", "}+><OptReturnType> with
    <Equations> end
  >
  
  Term.Ref      = <<QualId>>
  Term          = <<Sort>>
  Term          = <<Num>>
  Term.WldTerm  = <_>

  Term          = <(<Term>)> {bracket}

context-free syntax

  Arg.ImplicitArg = <(<Ident> := <Term>)>

context-free syntax

  Binder.Bind   = <<Name>>
  Binder.Binds  = <(<{Name " "}+>: <Term>)>
  Binder.Assign = <(<Name><OptType> := <Term>)>

context-free syntax

  Name          = <<Ident>>
  Name.WldName  = <_>
  
context-free syntax
	
	Sort.Prop     = <Prop>
	Sort.Set      = <Set>
	Sort.Type     = <Type>

context-free syntax // Auxiliary definitions of figure 1.1

  Equations.ListEqBar  = <
  | <{Equation "| "}+>
  >
  Equations.ListEq     = <
  <{Equation "| "}+>
  >
  
context-free syntax // Figure 1.2 of the reference manual

	FixBodies           = <<FixBody>>
 	FixBodies.With      = <<FixBody> with <{FixBody "with "}+> for <Ident>>

	FixBody.FixBody     = <
	<Ident> <{Binder " "}+><OptAnnotation><OptType> :=
		<Term>
	>

	CoFixBodies         = <<CoFixBody>>
 	CoFixBodies.With    = <<CoFixBody> with <{CoFixBody "with "}+> for <Ident>>

	CoFixBody.CoFixBody =	<<Ident><OptBinders><OptType> := <Term>>
	
	MatchItem.MatchItem = <<Term><OptAlias><OptInTerm>>
	
	ReturnType.Return    = <return <Term>>

	Equation.Eq        = [
  [{MultPattern " | "}+] => [Term]
  
  ]
	
	MultPattern        = <<{Pattern ", "}+>>
	
	Pattern.QidPattern = <<QualId> <{Pattern " "}+>>
	Pattern.AsPattern  = <<Pattern> as <Ident>>
	Pattern.XPattern   = <<Pattern> % <Ident>>
	Pattern            = <<QualId>>
	Pattern.Wld        = <_>
	Pattern            = <<Num>>
	Pattern.OrPattern  = <(<{OrPattern ", "}+>)>
	
	OrPattern          = <<{Pattern " | "}+>>
	
context-free syntax // Common notational extensions

  Term.List  = <[<{Term ";"}*>]> 
  Term.Tuple = <(<Term>,<{Term ","}+>)> 
  
  Term.Prod  = <<Term> * <Term>>  {left}
  Term.Eq    = <<Term> = <Term>>  {non-assoc}
  Term.NotE  = [[Term] <> [Term]] {non-assoc}
  Term.And   = <<Term> /\\ <Term>>

context-free priorities

  Term.ForAll > Term.Apply > Term.Arrow > Term.Prod > {non-assoc: Term.Check Term.Cast Term.Coerce Term.Eq Term.NotE} > Term.And

context-free syntax // 2.1 Used in records:

  Type = <<Term>>

	