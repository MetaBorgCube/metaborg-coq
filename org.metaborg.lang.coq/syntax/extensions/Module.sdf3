module extensions/Module

// 2.5 Module system

imports
  Vernacular
  Gallina
  Lexical
  Options
  Qid
  extensions/Section

lexical syntax

  Reserved = "Import"
  Reserved = "Export"
  Reserved = "Include"
  Reserved = "Inline"
  Reserved = "Module"
  Reserved = "End"
  Reserved = "Require"
  Reserved = "Type"

context-free syntax // Interactive modules (2.5.1 - 2.5.2)

  Module.InteractiveModule = <
  Module <OptModuleBindingKeywordPrefix><Ident><OptModuleBindings><OptModuleTypes>.
    <{ModContent "\n\n"}*>
  End <Ident>.
  >

context-free syntax

  ModuleBindingKeyword.Import = <Import>
  ModuleBindingKeyword.Export = <Export>

  OptModuleBindingKeywordPrefix.None = <>
  OptModuleBindingKeywordPrefix      = <<ModuleBindingKeyword> >
  
context-free syntax

  ModuleType.QualifiedModule        = <<QualId>>
  ModuleType.ModuleWithDefinition   = <<ModuleType> with Definition <QualId> := <Term>>
  ModuleType.ModuleWithModule       = <<ModuleType> with Module <QualId> := <QualId>>
  ModuleType.QualifiedModuleSeq     = <<QualId> <QualId+>>
  ModuleType.QualifiedModuleBangSeq = <!<QualId> <QualId+>>
  ModuleType                        = <(<ModuleType>)> {bracket}
  
  OptModuleTypes.None = <>
  OptModuleTypes      = < : <ModuleType>>
  OptModuleTypes      = [ <: [{ModuleType " <:"}*]]
  
context-free syntax

  ModuleBinding.ModuleBinding = <(<OptModuleBindingKeywordPrefix><Ident+> <ModuleType>)>
  
  OptModuleBindings.None = <>
  OptModuleBindings      = < <{ModuleBinding " "}+>>

context-free syntax // Encoding elements valid inside modules only

  ModContent         = <<Sentence>>
  ModContent         = <<Module>>
  ModContent.Include = <Include <{Ident "<+"}+>>
  
context-free syntax // Module definition (2.5.3)

  Module.ModuleDef = <
  Module <Ident><OptModuleBindings><OptModuleTypes> := <ModuleExpression>.
  > {prefer}
  Module.ModuleDefs = <
  Module <Ident><OptModuleBindings> := <{ModuleExpression "<+"}+>
  >
  
context-free syntax

  ModuleExpression.ModuleExpression     =  <<QualId+>>
  ModuleExpression.ModuleBangExpression = <!<QualId+>>
  
context-free syntax // Interactive Module Type definitions (2.5.4 & 2.5.5)

  Module.ModuleType = <
  Module Type <Ident><OptModuleBindings>.
    <ModTypeContent*>
  End <Ident>.
  >
  
context-free syntax

  ModTypeContent                  = <<ModContent>>
  ModTypeContent.InlineAssumption = <<AssumptionKeyword> Inline <Assums>>
  
context-free syntax // Module type equality (2.5.6)

  Module.ModuleTypeDef = <
  Module Type <Ident><OptModuleBindings> := <{ModuleType " <+"}+>
  >

context-free syntax // Imports (2.5.8)

  Sentence.Import = <<OptRequirePrefix>Import <QualId>.>
  Sentence.Export = <<OptRequirePrefix>Export <QualId>.>
  
context-free syntax

  OptRequirePrefix.None = <>
  OptRequirePrefix      = <Require >
